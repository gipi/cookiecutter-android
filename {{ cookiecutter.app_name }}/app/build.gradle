apply plugin: 'com.android.application'
apply plugin: 'com.android.databinding'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.neenbedankt.android-apt'

def getEndpoint = { ->
    final NGROK_ENDPOINT = 'http://127.0.0.1:4040'
    def url

    try {
        def http = new groovyx.net.http.HTTPBuilder(NGROK_ENDPOINT)
        def result = http.get(path: '/api/tunnels')
        result.tunnels.each { it ->
            if ('https' == it.proto) {
                url = it.public_url
            }
        }

    } catch (ignored) {
        url = 'https://user.forno.quintoandar.com.br'
    }

    return url
}

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId '{{ cookiecutter.package_name }}'
        versionCode 1
        versionName '1.0'

        minSdkVersion 16
        targetSdkVersion 23
        buildConfigField 'String', 'ENDPOINT', "\"${getEndpoint}\""
    }
    productFlavors {
        dev {
            minSdkVersion 21
        }
        prod {
            minSdkVersion 14
        }
    }

    buildTypes {
        debug {

        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'

    compile 'com.annimon:stream:1.0.5'

    apt 'com.google.dagger:dagger-compiler:2.0'
    compile 'com.google.dagger:dagger:2.0'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.retrofit:adapter-rxjava:2.0.0-beta2'
    compile 'io.reactivex:rxandroid:1.0.1'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile 'io.reactivex:rxjava:1.0.16'

    compile 'com.squareup.picasso:picasso:2.4.0'

    compile 'com.squareup:otto:1.3.8'

    compile 'org.projectlombok:lombok:1.16.6'

    compile 'com.jakewharton:butterknife:7.0.1'

    compile 'io.realm:realm-android:0.85.1'

    compile 'com.github.orhanobut:hawk:1.20'

    compile 'com.jakewharton.timber:timber:3.1.0'

    compile 'org.parceler:parceler-api:1.0.4'
    apt 'org.parceler:parceler:1.0.4'
}
